#
# Copyright (c) 2007-2014 Kaazing Corporation. All rights reserved.
# 
# Licensed to the Apache Software Foundation (ASF) under one
# or more contributor license agreements.  See the NOTICE file
# distributed with this work for additional information
# regarding copyright ownership.  The ASF licenses this file
# to you under the Apache License, Version 2.0 (the
# "License"); you may not use this file except in compliance
# with the License.  You may obtain a copy of the License at
# 
#   http://www.apache.org/licenses/LICENSE-2.0
# 
# Unless required by applicable law or agreed to in writing,
# software distributed under the License is distributed on an
# "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
# KIND, either express or implied.  See the License for the
# specific language governing permissions and limitations
# under the License.
#

# Autobahn test case 6.2.3

# Case Description
# Send a valid UTF-8 text message in fragments of 1 octet, resulting in frames ending on positions which are not code point ends.
# MESSAGE:
# 48656c6c6f2dc2b540c39fc3b6c3a4c3bcc3a0c3a12d5554462d382121

# Case Expectation
# The message is echo'ed back to us.

accept tcp://localhost:8001
accepted
connected

read "GET /echo/;e/cookies?.krn="
read /\d*/ "&.kl=Y HTTP/1.1" "\r\n"
read /User-Agent: .*/ "\r\n"
read "Host: localhost:8001\r\n"
read "Accept: text/html, image/gif, image/jpeg, *; q=.2, */*; q=.2\r\n"
read "Connection: keep-alive\r\n"
read "\r\n"

write "HTTP/1.1 200 OK\r\n"
write "Content-Type: text/plain; charset=UTF-8\r\n"
write "Date: Fri, 06 Jun 2014 18:22:04 GMT\r\n"
write "Location: http://localhost:8001/echo/;e/cookies?.kl=Y\r\n"
write "Server: Kaazing Gateway\r\n"
write "Content-Length: 0\r\n"
write "\r\n"

close
closed

accepted
connected

read "GET /echo?.kl=Y HTTP/1.1\r\n"
read "Upgrade: websocket\r\n"
read "Connection: Upgrade\r\n"
read "Host: localhost:8001\r\n"
read "Origin: privileged://localhost:8001\r\n"
read "Sec-WebSocket-Version: 13\r\n"
read /Sec-WebSocket-Key: (?<webSocketKey>.+)/ "\r\n"
read "Sec-WebSocket-Protocol: x-kaazing-handshake\r\n"
read "\r\n"

write "HTTP/1.1 101 Web Socket Protocol Handshake\r\n"
write "Connection: Upgrade\r\n"
write "Date: Fri, 06 Jun 2014 18:22:04 GMT\r\n"
write "Sec-WebSocket-Accept: "
write ${ws:handshakeHash(webSocketKey)}
write "\r\n"
write "Server: Kaazing Gateway\r\n"
write "Upgrade: WebSocket\r\n"
write "\r\n"

# Websocket text with valid utf8 payload in one octet fragments
write [0x01 0x01 0x48]
write [0x00 0x01 0x65]
write [0x00 0x01 0x6c]
write [0x00 0x01 0x6c]
write [0x00 0x01 0x6f]
write [0x00 0x01 0x2d]
write [0x00 0x01 0xc2]
write [0x00 0x01 0xb5]
write [0x00 0x01 0x40]
write [0x00 0x01 0xc3]
write [0x00 0x01 0x9f]
write [0x00 0x01 0xc3]
write [0x00 0x01 0xb6]
write [0x00 0x01 0xc3]
write [0x00 0x01 0xa4]
write [0x00 0x01 0xc3]
write [0x00 0x01 0xbc]
write [0x00 0x01 0xc3]
write [0x00 0x01 0xa0]
write [0x00 0x01 0xc3]
write [0x00 0x01 0xa1]
write [0x00 0x01 0x2d]
write [0x00 0x01 0x55]
write [0x00 0x01 0x54]
write [0x00 0x01 0x46]
write [0x00 0x01 0x2d]
write [0x00 0x01 0x38]
write [0x00 0x01 0x21]
write [0x00 0x01 0x21]
write [0x80 0x00]

# Websocket text with valid utf 8 payload
read [0x81 0x9d] 
read [(:maskingKey){4}]
read option mask ${maskingKey}
read [0x48 0x65 0x6c 0x6c 0x6f 0x2d 0xc2 0xb5 0x40 0xc3 0x9f 0xc3 0xb6 0xc3 0xa4 0xc3 0xbc 0xc3 0xa0 0xc3 0xa1 0x2d 0x55 0x54 0x46 0x2d 0x38 0x21 0x21]
read option mask [0x00]

# Websocket close with normal closure
write [0x88 0x02 0x03 0xe8]
read [0x88 0x82]
read [(:maskingKey){4}]
read option mask ${maskingKey}
read [0x03 0xe8]
read option mask [0x00]

close
closed