#
# Copyright (c) 2007-2014 Kaazing Corporation. All rights reserved.
# 
# Licensed to the Apache Software Foundation (ASF) under one
# or more contributor license agreements.  See the NOTICE file
# distributed with this work for additional information
# regarding copyright ownership.  The ASF licenses this file
# to you under the Apache License, Version 2.0 (the
# "License"); you may not use this file except in compliance
# with the License.  You may obtain a copy of the License at
# 
#   http://www.apache.org/licenses/LICENSE-2.0
# 
# Unless required by applicable law or agreed to in writing,
# software distributed under the License is distributed on an
# "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
# KIND, either express or implied.  See the License for the
# specific language governing permissions and limitations
# under the License.
#

# The script does not respond to the extended handshake request eventually resulting in connect timeout to expire

accept tcp://localhost:8001
accepted 
connected

read "GET /echo/;e/cookies?.krn="
read /\d*/ "&.kl=Y HTTP/1.1" "\r\n"
read /User-Agent: .*/ "\r\n"
read "Host: localhost:8001\r\n"
read "Accept: text/html, image/gif, image/jpeg, *; q=.2, */*; q=.2\r\n"
read "Connection: keep-alive\r\n"
read "\r\n"

write "HTTP/1.1 200 OK\r\n"
write "Content-Type: text/plain; charset=UTF-8\r\n"
write "Location: http://localhost:8001/echo/;e/cookies?.kl=Y\r\n"
write "Server: Kaazing Gateway\r\n"
write "Date: Fri, 26 Apr 2013 04:48:46 GMT\r\n"
write "Content-Length: 0\r\n"
write "\r\n"

# THIS REALLY NEEDS A HTTP TRANSPORT BECAUSE 
# WE ARE FORCING ONE TCP PATH WHERE TWO ARE VALID
close
closed 

accepted
connected

# CONNECT
read "GET /echo?.kl=Y HTTP/1.1\r\n"
read "Upgrade: websocket\r\n"
read "Connection: Upgrade\r\n"
read "Host: localhost:8001\r\n"
read "Origin: privileged://localhost:8001\r\n"
read "Sec-WebSocket-Version: 13\r\n"
read /Sec-WebSocket-Key: (?<webSocketKey>.+)/ "\r\n"
read "Sec-WebSocket-Protocol: x-kaazing-handshake\r\n"
read "\r\n"

# WS native CONNECT
write "HTTP/1.1 101 Web Socket Protocol Handshake\r\n"
write "Connection: Upgrade\r\n"
write "Date: Fri, 24 Jan 2014 12:50:58 GMT\r\n"
write "Sec-WebSocket-Accept: "
write ${ws:handshakeHash(webSocketKey)}
write "\r\n"
write "Sec-WebSocket-Protocol: x-kaazing-handshake\r\n"
write "Server: Kaazing Gateway\r\n"
write "Upgrade: websocket\r\n"

write "\r\n"
write [0x82 0x04 0xEF 0x83 0xBF 0x4E]

# Negotiate extensions
read [0x82 0xc8]
read [(:maskingKey){4}]
read option mask ${maskingKey}
read "GET /echo HTTP/1.1\r\n"
read "Sec-WebSocket-Extensions: x-kaazing-idle-timeout\r\n"
read "\r\n"
read option mask [0x00]

# Client should close the WebSocket connection since the connect timeout expires
# DISCONNECT
read [0x88 0x80]
read option mask ${maskingKey}
read option mask [0x00]

# DISCONNECTED
write [0x88 0x00]

close
closed
